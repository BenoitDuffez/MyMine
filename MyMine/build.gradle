apply plugin: 'android'

dependencies {
    // Libraries from maven
    compile 'com.intellij:annotations:+@jar'
    compile 'com.nhaarman.listviewanimations:library:2.5.2'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.4.0'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:extra-abc:0.9'
    compile 'net.java:textile-j:2.2'
    compile 'ch.acra:acra:4.5.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'asia.ivity.android:drag-sort-listview:1.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.8.6'
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.android.support:support-v4:+'
    compile('de.keyboardsurfer.android.widget:crouton:1.8.1') {
        exclude module: 'support-v4'
        compile SupportLib
    }
    repositories {
        mavenCentral()
    }

    // Support Libraries
    compile SupportLib
    compile AppCompatLib

    // Projects
    compile project(':SplitActivity')
    compile project(':aFileChooser')
    compile project(':sgv')

    // Jars
    compile files('libs/libGoogleAnalyticsServices.jar')
}

android {
    compileSdkVersion CompileSdkVersion
    buildToolsVersion BuildToolsVersion

    defaultConfig {
        minSdkVersion MinSdkVersion
        targetSdkVersion TargetSdkVersion
    }

    signingConfigs {
        releasePlayStore
    }

    buildTypes {
        releasePlayStore {
            debuggable false
            jniDebugBuild false
            signingConfig signingConfigs.releasePlayStore
        }
        releaseAlpha {
            debuggable false
            jniDebugBuild false
            signingConfig signingConfigs.releasePlayStore
            lintOptions.abortOnError false
        }
        debug {
            versionNameSuffix "-dev"
        }
    }

}

def Properties props = new Properties()
def propFile = file('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.releasePlayStore.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.releasePlayStore.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.releasePlayStore.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.releasePlayStore.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.releasePlayStore.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.releasePlayStore.signingConfig = null
}

